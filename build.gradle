plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id "com.adarshr.test-logger" version "3.2.0"
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

group = 'io.github.drednote'
version = findProperty("tag_version") ?: 'master-SNAPSHOT'
sourceCompatibility = '17'

bootJar.enabled = false

jar {
    enabled = true
    archiveClassifier = ''
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.telegram:telegrambots-longpolling:9.0.0'
    api 'org.telegram:telegrambots-client:9.0.0'

    api 'com.esotericsoftware:kryo-shaded:4.0.3'
    api 'com.github.vladimir-bukhtoyarov:bucket4j-core:7.6.0'
    api 'com.github.ben-manes.caffeine:caffeine'
    api 'org.apache.httpcomponents.client5:httpclient5'

    api 'org.springframework.boot:spring-boot-starter-web'

    api 'org.springframework.statemachine:spring-statemachine-core:4.0.0'
    api 'org.springframework.statemachine:spring-statemachine-data-common:4.0.0'

    // data
    compileOnly 'org.springframework.boot:spring-boot-starter-data-jpa'
    compileOnly 'org.springframework.boot:spring-boot-starter-data-mongodb'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation 'org.springframework.boot:spring-boot-starter-validation'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    testImplementation 'org.projectlombok:lombok'
    testImplementation 'io.zonky.test:embedded-database-spring-test:2.5.1'
    testImplementation 'io.zonky.test:embedded-postgres:2.0.7'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo.spring3x:4.20.0'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.register('sourceJar', Jar) {
    from sourceSets.main.allJava
}

tasks.named('compileJava') {
    inputs.files(tasks.named('processResources'))
}

signing {
    if (!version.toString().endsWith("SNAPSHOT")) {
        useGpgCmd()
        sign publishing.publications
    }
}

publishing {
    repositories {
        maven {
            allowInsecureProtocol true
            url = NEXUS_UPLOAD_REPO
            credentials {
                username = LOCAL_NEXUS_USER
                password = LOCAL_NEXUS_PASSWORD
            }
        }
    }
    publications {
        register('mavenJava', MavenPublication) {
            groupId group
            artifactId archivesBaseName
            version version

            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            pom {
                packaging = jar
                name = "Spring Boot Starter Telegram"
                url = "https://github.com/drednote/spring-boot-starter-telegram"
                description = "A library designed to simplify the setup of Telegram bots using " +
                        "`Spring Boot` and `org.telegram:telegrambots` as the core dependency"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/Drednote/spring-boot-starter-telegram/blob/master/LICENSE"
                    }
                }

                scm {
                    connection = "scm:https://github.com/drednote/spring-boot-starter-telegram.git"
                    developerConnection = "scm:git@github.com:drednote/spring-boot-starter-telegram.git"
                    url = "https://github.com/drednote/spring-boot-starter-telegram"
                }

                developers {
                    developer {
                        name = "Ivan Galushko"
                        email = "galushko.ivan8@gmail.com"
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri(MAVEN_RELEASE_REPO))
            snapshotRepositoryUrl.set(uri(NEXUS_UPLOAD_REPO))
            username.set(System.getenv("CENTRAL_PORTAL_USERNAME") ?: System.getProperty("CENTRAL_PORTAL_USERNAME"))
            password.set(System.getenv("CENTRAL_PORTAL_PASSWORD") ?: System.getProperty("CENTRAL_PORTAL_PASSWORD"))
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    jacocoTestReport {
        dependsOn test
        reports {
            xml.enabled true
            csv.enabled false
            html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
        }
    }
}
