plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'io.github.drednote'
version = '0.1.0.Beta1.SNAPSHOT'
sourceCompatibility = '17'

bootJar.enabled = false

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.telegram:telegrambots:6.5.0'
    api 'com.esotericsoftware:kryo:4.0.3'

    // data
    compileOnly 'org.springframework.boot:spring-boot-starter-data-jpa'
    compileOnly 'org.springframework.boot:spring-boot-starter-data-mongodb'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    testImplementation 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.register('sourceJar', Jar) {
    from sourceSets.main.allJava
}

signing {
    if (!version.endsWith('SNAPSHOT')) {
        useGpgCmd()
        sign publishing.publications.mavenJava
    }
}

publishing {
    repositories {
        maven {
            def releasesUrl = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            def snapshotsUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            url = (version.toString().endsWith("SNAPSHOT")) ? snapshotsUrl : releasesUrl
            credentials {
                username = project.properties["ossrhUsername"].toString()
                password = project.properties["ossrhPassword"].toString()
            }
        }
    }
    publications {
        register('mavenJava', MavenPublication) {
            groupId group
            artifactId archivesBaseName
            version System.getProperty("generatedVersion") ?: version

            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            pom {
                packaging = jar
                name = "Spring Boot Starter Telegram"
                url = "https://github.com/drednote/spring-boot-starter-telegram"
                description = "A library designed to simplify the setup of Telegram bots using " +
                        "`Spring Boot` and `org.telegram:telegrambots` as the core dependency"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/drednote/spring-boot-starter-telegram/LICENSE"
                    }
                }

                developers {
                    developer {
                        name = "Galushko Ivan"
                        email = "galushko.ivan8@gmail.com"
                    }
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    jacocoTestReport {
        dependsOn test
        reports {
            xml.enabled false
            csv.enabled false
            html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
        }
    }
}
